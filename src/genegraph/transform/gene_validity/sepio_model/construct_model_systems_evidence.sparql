prefix gci: <https://genegraph.clinicalgenome.org/r/>
prefix gcixform: <https://genegraph.clinicalgenome.org/r/gcixform/>
prefix cg: <https://genegraph.clinicalgenome.org/terms/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

construct {
  ?evidenceLine a cg:EvidenceLine ;
  cg:specifiedBy ?criteria ;
  dc:description ?scoreDescription ;
  cg:evidence ?evidenceItem ;
  cg:direction ?scoreStatus ;
  cg:calculatedScore ?calculatedScore ;
  cg:strengthScore ?score .

  # TODO Grab the model organism from the data
  ?evidenceItem a cg:Finding ;
  cg:demonstrates cg:ProteinAlterationDisruptsOrganismFunction ;
  dc:description ?evidenceDescription ;
  dc:source ?article ;
  rdfs:label ?evidenceLabel .
}
where {

  ?evidenceLine a gci:evidenceScore ;
  gci:calculatedScore ?calculatedScore ;
  gci:scoreStatus ?scoreStatus ;
  gci:affiliation ?affiliation .

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?scoreDescription .
  }

  OPTIONAL {
    ?evidenceLine gci:score ?adjustedScore .
  }

  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?score) .
  
  ?evidenceItem gci:scores ?evidenceLine ;
  gci:modelSystems ?gciModelSystem  ;
  gci:label ?evidenceLabel .
  
  ?gciModelSystem gci:explanation ?evidenceDescription ;
  gci:modelSystemsType ?gciType .

  ?criteria a cg:Criteria ;
  gcixform:hasGCIType ?gciType ;
  gcixform:usedIn gcixform:ModelSystems .
  
  ?annotation gci:experimentalData ?evidenceItem ;
  gci:article ?publication .
  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .
 
}
