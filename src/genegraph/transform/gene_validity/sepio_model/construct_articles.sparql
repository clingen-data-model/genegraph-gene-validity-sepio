prefix gci: <https://genegraph.clinicalgenome.org/r/>
prefix gcixform: <https://genegraph.clinicalgenome.org/r/gcixform/>
prefix ga4gh: <https://terms.ga4gh.org/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct
{
  ?article a dc:BibliographicResource ;
  dc:title ?title ;
  dc:creator ?firstAuthor ;
  dc:date ?year ;
  dc:abstract ?abstract .
}
where
{
  ?gci_article a gci:article ;
  gci:date ?gci_date ;
  gci:pmid ?pmid .
  OPTIONAL {
    ?gci_article gci:title ?title ;
  }


  OPTIONAL {
    ?gci_article gci:authors ?authors .
    ?authors rdf:first ?firstAuthor ;
    rdf:rest ?otherAuthors .
    ?otherAuthors rdf:first ?nextAuthor .
  }
  BIND(IF(BOUND(?nextAuthor) && STRLEN(?nextAuthor)>0, true, false) AS ?multipleAuthors) .

  OPTIONAL {
    ?gci_article gci:abstract ?abstract .
  }
    
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  BIND(SUBSTR(?gci_date, 1, 4) AS ?year) .
}
