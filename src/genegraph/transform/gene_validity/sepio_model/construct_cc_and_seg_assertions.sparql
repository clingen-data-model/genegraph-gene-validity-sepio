prefix gci: <https://genegraph.clinicalgenome.org/r/gci/>
prefix cg: <https://genegraph.clinicalgenome.org/r/terms/>

construct
{
  ?geneticEvidenceLine cg:evidence ?caseControlEvidenceLine , ?segregationEvidenceLine .

  ?caseControlEvidenceLine a cg:EvidenceLine ;
  cg:specifiedBy cg:GeneValidityOverallCaseControlEvidenceCriteria ;
  cg:strengthScore ?caseControlTotal .

  ?segregationEvidenceLine a cg:EvidenceLine ;
  cg:specifiedBy cg:GeneValiditySegregationEvidencCriteria ;
  cg:strengthScore ?segregationTotal .
  
}
where
{

  ?classification a gci:provisionalClassification ;
  gci:classificationPoints ?pointsTree ;
  gci:approvedClassification true .

  BIND (IRI(CONCAT(str(?classification), "_overall_genetic_evidence_line")) AS ?geneticEvidenceLine) .
  
  ?pointsTree gci:segregation ?segregationTree .
  ?segregationTree gci:pointsCounted ?segregationTotal .

  ?pointsTree gci:caseControl ?caseControlTree .
  ?caseControlTree gci:pointsCounted ?caseControlTotal .

  BIND (IRI(CONCAT(str(?classification), "_cc_el")) AS ?caseControlEvidenceLine) .
  BIND (IRI(CONCAT(str(?classification), "_seg_el")) AS ?segregationEvidenceLine) .
}
