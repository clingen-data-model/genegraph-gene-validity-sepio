prefix gci: <https://genegraph.clinicalgenome.org/r/>
prefix gcixform: <https://genegraph.clinicalgenome.org/r/gcixform/>
prefix cg: <https://genegraph.clinicalgenome.org/terms/>
prefix dc: <http://purl.org/dc/terms/>

construct {
  ?evidenceLine a cg:UnscoreableEvidence ;
  cg:evidence ?article ;
  dc:description ?description .
  
}
where {
  ?evidenceLine a gci:annotation ;
  gci:articleNotes / gci:nonscorable ?nonscorable .

  ?nonscorable gci:checked true .
  
  OPTIONAL {
      ?nonscorable gci:text ?description . 
  }

  OPTIONAL {
    ?evidenceLine gci:article / gci:pmid ?pmid .
    BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .
  }

  OPTIONAL {
    ?evidenceLine gci:scv / gci:scvId ?scvid .
    BIND(IRI(CONCAT(?scvbase, ?scvid)) AS ?article) .
  }

}
