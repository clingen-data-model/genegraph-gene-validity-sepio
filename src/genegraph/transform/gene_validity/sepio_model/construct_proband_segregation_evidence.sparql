prefix gci: <https://genegraph.clinicalgenome.org/r/>
prefix gcixform: <https://genegraph.clinicalgenome.org/r/gcixform/>
prefix cg: <https://genegraph.clinicalgenome.org/terms/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

construct {
  ?segregationID a cg:FamilyCosegregation ;
  cg:family ?family ;
  dc:description ?scoreDescription ;
  cg:sequencingMethod ?sequencingMethod ;
  dc:source ?article ;
  cg:estimatedLodScore ?estimatedLodScore ;
  cg:publishedLodScore ?publishedLodScore ;
  cg:phenotypePositiveAllelePositive ?affected ;
  cg:phenotypeNegativeAlleleNegative ?unaffected ;
  cg:meetsInclusionCriteria ?meetsCriteria ;
  cg:phenotypes ?phenotypes ;
  cg:phenotypeFreeText ?phenotypeFreeText ;
  rdfs:label ?evidenceLabel ;
  cg:proband ?proband .

  ?family a cg:Family ;
  rdfs:label ?evidenceLabel ;
  cg:modeOfInheritance ?familyMoi ;
  cg:ethnicity ?familyEthnicity .

  ?segregationEvidenceLine cg:evidence ?segregationID .
}
where {

  ?classification a gci:provisionalClassification ;
  gci:approvedClassification true .
  BIND (IRI(CONCAT(str(?classification), "_seg_el")) AS ?segregationEvidenceLine) .

  ################
  
  ?family a gci:family ;
  gci:segregation ?segregation ;
  gci:label ?evidenceLabel .

  BIND(IRI(CONCAT(STR(?family), "_proband_segregation")) AS ?segregationID) .

  # Limit to scored segregations -- need to consider how to incorporate this
  OPTIONAL {
    ?segregation gci:includeLodScoreInAggregateCalculation ?meetsCriteria .
  }
  
  OPTIONAL {
    ?segregation gci:sequencingMethod ?sequencingMethod .
  }

  ?annotation gci:families | ( gci:groups / gci:familyIncluded ) ?family ;
  gci:article ?publication .
  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  optional {
    ?segregation gci:publishedLodScore ?publishedLodScore .
  }

  optional {
    ?segregation gci:estimatedLodScore ?estimatedLodScore .
  }

  optional {
    ?segregation gci:reasonExplanation ?scoreDescription ;
  }
  
  optional {
    ?segregation gci:numberOfAffectedWithGenotype ?affected .
  }

  optional {
    ?family gci:hpoIdInDiagnosis ?phenotypes .
  }

  OPTIONAL {
     ?family gci:termsInDiagnosis ?phenotypeFreeText .
  }

  OPTIONAL {
    ?family gci:ethnicity ?familyEthnicity .
  }

  # NB, this seems specific to AR curations, don't forget to check AD .
  optional {
    ?segregation gci:numberOfUnaffectedWithoutBiallelicGenotype ?unaffected .
  }

  OPTIONAL {
    ?segregation gci:moiDisplayedForFamily ?familyMoi .
  }

  ?family gci:individualIncluded ?proband .
  ?proband gci:proband true .
  { ?proband gci:variantScores ?scores }
  UNION
  { FILTER NOT EXISTS { [] gci:variantScores [] }
    ?proband gci:scores ?scores }
  FILTER NOT EXISTS { ?scores gci:scoreStatus <http://purl.obolibrary.org/obo/SEPIO_0004585> }
  ?scores gci:affiliation ?affiliation .
  ?scores gci:scoreStatus ?scoreStatus .
}
