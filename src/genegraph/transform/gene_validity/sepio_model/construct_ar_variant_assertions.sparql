prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix cg: <http://dataexchange.clinicalgenome.org/terms/>

construct
{
  ?geneticEvidenceLine cg:evidence
  ?autosomalRecessiveVariantEvidenceLine .
  
  ?autosomalRecessiveVariantEvidenceLine a cg:EvidenceLine ;
  cg:specifiedBy cg:GeneValidityOverallAutosomalRecessiveVariantEvidenceCriteria ;
  cg:strengthScore ?autosomalRecessiveDisorderTotal .  
}
where
{
  ?classification a gci:provisionalClassification ;
  gci:approvedClassification true ;
  gci:classificationPoints ?pointsTree .

  BIND (IRI(CONCAT(str(?classification), "_overall_genetic_evidence_line")) AS ?geneticEvidenceLine) .

  ?pointsTree gci:autosomalRecessiveDisorder ?autosomalRecessiveDisorderTree .
  ?autosomalRecessiveDisorderTree gci:pointsCounted  ?autosomalRecessiveDisorderTotal .

  BIND (IRI(CONCAT(str(?classification), "_ar_el")) AS ?autosomalRecessiveVariantEvidenceLine) .

}
