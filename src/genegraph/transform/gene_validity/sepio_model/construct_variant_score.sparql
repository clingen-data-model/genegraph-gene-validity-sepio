# Handles SOP7.v2 and SOP8
prefix gci: <https://genegraph.clinicalgenome.org/r/gci/>
prefix gcixform: <https://genegraph.clinicalgenome.org/r/gcixform/>
prefix sepio: <http://purl.obolibrary.org/obo/SEPIO_>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix cg: <https://genegraph.clinicalgenome.org/r/terms/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

construct {
  ?evidenceLine a cg:EvidenceLine ;
  cg:strengthScore ?score ;
  cg:evidence ?evidenceItem , ?functionEvidenceItem ; # evidence about variant
  cg:calculatedScore ?resultCalculatedScore ;
  cg:direction ?scoreStatus ;
  dc:description ?evidenceLineDescription .

# Variant evidence item
  ?evidenceItem a cg:VariantObservation ;
  cg:allele ?variant ;
  dc:source ?article ;
  cg:alleleOrigin ?alleleOrigin ;
  cg:paternityMaternityConfirmed ?paternityMaternityConfirmed ;
  cg:zygosity ?zygosity .
  
# Proband with variant evidence item
  ?individual a cg:Proband ;
  cg:allele ?variant ;
  cg:sex ?sex ;
  cg:ageUnit ?ageUnit ;
  cg:ageType ?ageType ;
  cg:ageValue ?ageValue ;
  cg:ethnicity ?ethnicity ; 
  rdfs:label ?individualLabel ;
  cg:previousTesting ?prevTesting ;
  cg:previousTestingDescription ?prevTestingDescription ;
  cg:firstTestingMethod ?firstTestingMethod ;
  cg:secondTestingMethod ?secondTestingMethod ;  
  cg:detectionMethod ?methodsOfDetection ;
  cg:phenotypeFreeText ?phenotypeFreeText ;
  cg:phenotypes ?phenotypes ;
  cg:zygosity ?probandZygosity ;
  cg:variant ?evidenceItem .

  ?family a cg:Family ; # http://purl.obolibrary.org/obo/PCO_0000020
  rdfs:label ?familyLabel ;
  cg:member ?individual .

  ?functionEvidenceItem a cg:VariantFunctionalImpactEvidence ;
  dc:description ?functionalDataExplanation ;
  cg:functionalDataSupport ?functionalDataSupport .
}
where {

  ?evidenceLine a gci:variantScore ;
  gci:variantScored ?variant ;
  gci:deNovo ?deNovo ;
  gci:scoreStatus ?scoreStatus ;
  gci:date_created ?date_created ;
  gci:affiliation ?affiliation .
  
  # maschinations due to contradicts score_status do not have gci:caseInfoType
  OPTIONAL {
    ?evidenceLine gci:variantType ?variantType .
    ?variantType gcixform:hasEvidenceLineType ?evidenceLineType ;
    gcixform:hasEvidenceItemType ?evidenceItemType .
  }


  # TODO -- fix this; this escaped the purge of SEPIO types
  # NonNullVariantEvidenceLine
  BIND(IF(BOUND(?variantType), ?evidenceLineType, sepio:0004097) AS ?lineType) .
  # VariantEvidenceItem
  BIND(IF(BOUND(?variantType), ?evidenceItemType, sepio:0004116) AS ?itemType) .

  OPTIONAL {
    ?evidenceLine gci:calculatedScore ?calculatedScore .
  }

  # This is not actually a proband score, but rather a variant score
  # Should name it as such =tristan
  # BIND (IRI(CONCAT(str(?evidenceLine), "_proband_score_evidence_line")) AS ?probandScoreEvidenceLine) 

  BIND(IF(?deNovo = "Yes", :geno/DeNovoAlleleOrigin , IF(?deNovo = "No", :geno/GermlineAlleleOrigin , :geno/AlleleOrigin ) ) AS ?alleleOrigin) .

  OPTIONAL {
      ?evidenceLine gci:maternityPaternityConfirmed ?paternityMaternityConfirmed .
  }

  OPTIONAL {
    ?evidenceLine gci:functionalDataExplanation ?functionalDataExplanation ;
    BIND(IRI(CONCAT(STR(?evidenceLine), "_function_evidence_item")) AS ?functionEvidenceItem) .
  }

  OPTIONAL {
    ?evidenceLine gci:functionalDataSupport ?functionalDataSupport .
  }

  BIND(IRI(CONCAT(STR(?evidenceLine), "_variant_evidence_item")) AS ?evidenceItem) .

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?evidenceLineDescription .
  }
  
  OPTIONAL  {
    ?evidenceLine gci:score ?adjustedScore .
  }
  
  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?baseScore) .

  ?individual gci:variantScores ?evidenceLine ;
  gci:label ?individualLabel ;
  gci:proband true .

  OPTIONAL {
    ?individual gci:sex ?sex .
  }

  OPTIONAL {
    ?individual gci:ageType ?ageType .
  }

  OPTIONAL {
    ?individual gci:ageValue ?ageValue .
  }

  OPTIONAL {
    ?individual gci:ageUnit ?ageUnit .
  }

  OPTIONAL {
    ?individual gci:hpoIdInDiagnosis ?phenotypes .
  }

  OPTIONAL {
     ?individual gci:probandIs ?probandZygosity .
  }

  OPTIONAL {
    ?individual gci:ethnicity ?ethnicity .
  }

  ?individual gci:method ?method .

  OPTIONAL {
    ?method gci:genotypingMethods / rdf:rest{0} / rdf:first ?firstTestingMethod .
  }

  OPTIONAL {
    ?method gci:genotypingMethods / rdf:rest{1} / rdf:first ?secondTestingMethod .
  }

  OPTIONAL {
    ?method gci:previousTesting ?prevTesting .
  }

 OPTIONAL {
     ?method gci:specificMutationsGenotypedMethod ?methodsOfDetection .
  }
  
  OPTIONAL {
    ?method gci:previousTestingDescription ?prevTestingDescription .
  }

  OPTIONAL {
     ?individual gci:termsInDiagnosis ?phenotypeFreeText .
  }

  # Below is a score fix based dependent on mode of inheritance, based on the
  # conversation with Gloria Cheung from Slack:
  
  # https://clingen.slack.com/archives/C03TR9RA4DP/p1665181171371129

#   Hi 
# @Tristan Nelson, here is my findings.
# Looks like most of the recessiveZygosity=“Homozygous” are left over data from previous SOPv7.For SOPv8, the recessiveZygosity value for Semidominant curation is evaluated as below in GCI. If probandIs is “Monoallelic heterozygous” or “Hemizygous” then treat as AUTOSOMAL_DOMINANT. And if “Hemizygous” is checked then recessiveZygosity=“Hemizyous”
# So in these cases, recessiveZygosity=“Homozygous” should not be evaluated (is not valid). In SOPv8, if “Hemizygous” is unchecked, recessiveZygosity attribute is removed.
# If probandIs is “Biallelic homozygous” or “Biallelic compound heterozygous” then treat as AUTOSOMAL_RECESSIVE. And recessiveZygosity=“TwoTrans” if “Check here if there are 2 variants AND they are both located in trans with respect to one another” option is checked. And recessiveZygosity=“Homozygous” if “Homozygous” option is checked. So in these cases, recessiveZygosity=“Homozygous” is used to consider how the point is counted. If “Biallelic homozygous” is selected, recessiveZygosity is forced to be “Homozygous”, or if “Biallelic compound heterozygous” is selected and “Homozygous” is checked then in these cases only one variant score can be added and score will be double counted.
# For example, user HOU336_904 and LU_II:7, both have “probandIs”=“Biallelic homozygous” and recessiveZygosity=“Homozygous” and have one variantScore which score is double counted.

  OPTIONAL {
    ?individual gci:recessiveZygosity ?recessiveZygosity .
  }

  OPTIONAL {
    ?individual gci:probandIs ?semidominantZygosity .
  }

  BIND(COALESCE(?semidominantZygosity, ?recessiveZygosity, :geno/Heterozygous ) AS ?zygosity) .
  BIND(IF( ?zygosity = :geno/Homozygous , ?baseScore * 2 , ?baseScore) AS ?score) .
  BIND(IF( ?zygosity = :geno/Homozygous , ?calculatedScore * 2 , ?calculatedScore) AS ?resultCalculatedScore) .

  OPTIONAL {
    ?annotation a gci:annotation ;
    (gci:families |
     gci:groups |
     gci:familyIncluded |
     gci:individuals |
     gci:individualIncluded ) * 
    ?individual .
  }

  OPTIONAL {
    ?annotation gci:article / gci:pmid ?pmid .
    BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .
  }

  OPTIONAL {
    ?annotation gci:scv / gci:scvId ?scvid .
    BIND(IRI(CONCAT(?scvbase, ?scvid)) AS ?article) .
  }
  
  OPTIONAL {
    ?family gci:individualIncluded ?individual ;
    gci:label ?familyLabel ;
    a gci:family .
  }


}
